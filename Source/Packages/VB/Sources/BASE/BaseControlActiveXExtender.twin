[ClassId("ADB5583B-5C11-4317-9541-B511ED17C718")]
[InterfaceId("4DCF51FC-8AA6-47EF-913A-B0D106F041A3")]
[COMCreatable(False)]
Private Class BaseControlActiveXExtender
 
    Inherits BaseControlRectDockable

    [Description("Indicates if the user can use the TAB key to focus on this control at runtime")]
    [Serialize(True, "TabStop")]
        Protected TabStopINIT As Boolean = True
    [Description("Adjusts the TAB key order of this control at runtime")]
    [Serialize(True, "TabIndex")]
        Protected TabIndexINIT As Long
        
    '[ Description ("") ]
    '    Public Visible As Boolean = True
        
    [Serialize(True, "DragIcon")]
    [CustomDesigner("designer_IconBytes")]
    [Description("")]
        Protected ReadOnly DragIconINIT() As Byte
    [Serialize(True, "DragMode")]
        Protected DragModeINIT As VBRUN.DragModeConstants
        
    [Serialize(True, "CausesValidation")]
        Protected CausesValidationINIT As Boolean

    Sub New(ByVal ControlType As ControlTypeConstants)
        BaseControlRectDockable.New(ControlType)
    End Sub
    Protected Sub CreateRootWindowElement(ByVal _ControlContext As ControlContext, _
                                               ByRef InitData As WindowCreationData)
        'Stop
        With InternalBaseControlInfo
            .DragMode = Me.DragModeINIT
            .CausesValidation = Me.CausesValidationINIT
            .TabStop = Me.TabStopINIT
            .HasTabIndex = True
            .TabIndex = Me.TabIndexINIT
        End With
        InternalBaseControlBeforeCreateRootWindow(InitData)
        Me.ControlContext = _ControlContext
        Me.RootWindowElementBase.Pointer = _ControlContext.RuntimeUICtxCreateWindowElement(InitData)
    End Sub
    
    Public Sub Drag([TypeHint(DragConstants)] Optional ByVal Action As Variant)
        CommonDrag(Action)
    End Sub
    
    [Serialize(False)]
    Public Property Get DragMode() As DragModeConstants
        Return InternalBaseControlInfo.DragMode
    End Property
    
    [Serialize(False)]
    Public Property Let DragMode(ByVal Value As DragModeConstants)
        InternalBaseControlInfo.DragMode = Value
        RootWindowElementBase.RuntimeUIDragModeChanged()
    End Property
    
    [Serialize(False)]
    Public Property Get CausesValidation() As Boolean
        Return InternalBaseControlInfo.CausesValidation
    End Property
    
    [Serialize(False)]
    Public Property Let CausesValidation(ByVal Value As Boolean)
        InternalBaseControlInfo.CausesValidation = Value
    End Property
    
    [Serialize(False)]
    [DispId(-854)]
    Public Property Get TabStop() As Boolean
        Return InternalBaseControlInfo.TabStop
    End Property
    
    [Serialize(False)]
    [DispId(-854)]
    Public Property Let TabStop(ByVal Value As Boolean)
        InternalBaseControlInfo.TabStop = Value
    End Property
    
    [Serialize(False)]
    Public Property Get TabIndex() As Long
        Return InternalBaseControlInfo.TabIndex
    End Property
    
    [Serialize(False)]
    Public Property Let TabIndex(ByVal Value As Long)
        InternalBaseControlInfo.TabIndex = Value
    End Property
    
    [Serialize(False)]
    Public Property Get DragIcon() As StdPicture
        Return InternalBaseControlInfo.DragIcon
    End Property

    [Serialize(False)]
    Public Property Set DragIcon(Value As StdPicture)
        Set InternalBaseControlInfo.DragIcon = Value
    End Property

    [Serialize(False)]
    Public Property Let DragIcon(Value As StdPicture)
        Set InternalBaseControlInfo.DragIcon = Value
    End Property
    
    Public Property Get Height() As Double
        Return BaseControlRectDockable.Height
    End Property
   
    Public Property Let Height(ByVal Value As Double)
        BaseControlRectDockable.Height = Value
        InternalSyncDesignerDimensions()
    End Property
   
    Public Property Get Width() As Double
        Return BaseControlRectDockable.Width
    End Property
   
    Public Property Let Width(ByVal Value As Double)
        BaseControlRectDockable.Width = Value
        InternalSyncDesignerDimensions()
    End Property
    
    Public Sub Move(ByVal Left As Single, Optional ByVal Top As Variant, Optional ByVal Width As Variant, Optional ByVal Height As Variant)
        BaseControlRectDockable.Move(Left, Top, Width, Height)
        'SetWindowPos(RootWindowElementBase.Handle, 0, CLng(PixelsLeft), CLng(PixelsTop), CLng(PixelsWidth), CLng(PixelsHeight), SWP_NOACTIVATE Or SWP_NOCOPYBITS Or SWP_NOOWNERZORDER Or SWP_NOSENDCHANGING Or SWP_NOZORDER)
        InternalSyncDesignerDimensions()
    End Sub
    
End Class