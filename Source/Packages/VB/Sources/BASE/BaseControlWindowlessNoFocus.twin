[ClassId("57858BD8-4A90-461A-94F2-6D6D5F855A36")]
[InterfaceId("629884A1-C2C5-4A32-AA4C-3733648E913D")]
[COMCreatable(False)]
Private Class BaseControlWindowlessNoFocus

    Inherits BaseControlRectDockable

    [Description("")]
    [Serialize(True, ":Visible")]
        Protected VisibleINIT As Boolean = True
        
    [Serialize(True, "DragIcon")]
    [CustomDesigner("designer_IconBytes")]
    [Description("")]
        Protected ReadOnly DragIconINIT() As Byte
    [Serialize(True, "DragMode")]
        Protected DragModeINIT As VBRUN.DragModeConstants
    
    Sub New(ByVal ControlType As ControlTypeConstants)
        BaseControlRectDockable.New(ControlType)
    End Sub

    Protected Sub CreateRootWindowElement(ByVal _ControlContext As ControlContext, _
                                                ByRef InitData As WindowCreationData)
        With InternalBaseControlInfo
            .WindowlessVisible = Me.VisibleINIT
            .DragMode = Me.DragModeINIT
        End With
        InternalBaseControlBeforeCreateRootWindow(InitData)
        Me.ControlContext =_ControlContext
        Me.RootWindowElementBase.Pointer = ControlContext.RuntimeUICtxCreateWindowElement(InitData)
        CommonLoadPictureInit(InternalBaseControlInfo.DragIcon, Me.DragIconINIT)
    End Sub
            
    Public Sub Drag([TypeHint(DragConstants)] Optional ByVal Action As Variant)
        CommonDrag(Action)
    End Sub

    Public Sub Refresh()
        WindowlessRefresh()
    End Sub
    
    Public Sub ZOrder([TypeHint(ZOrderConstants)] Optional ByVal Position As Variant)
        CommonZOrderWindowless(Position, Me.ControlContext)
    End Sub
    
    [Serialize(False)]
    Property Get Visible() As Boolean
        Return InternalBaseControlInfo.WindowlessVisible
    End Property
        
    [Serialize(False)]
    Property Let Visible(ByVal Value As Boolean)
        InternalBaseControlInfo.WindowlessVisible = Value
        WindowlessRefresh()
    End Property
    
    [Serialize(False)]
    Public Property Get DragMode() As DragModeConstants
        Return InternalBaseControlInfo.DragMode
    End Property
    
    [Serialize(False)]
    Public Property Let DragMode(ByVal Value As DragModeConstants)
        InternalBaseControlInfo.DragMode = Value
        RootWindowElementBase.RuntimeUIDragModeChanged()
    End Property
    
    [Serialize(False)]
    Public Property Get DragIcon() As StdPicture
        Return InternalBaseControlInfo.DragIcon
    End Property

    [Serialize(False)]
    Public Property Set DragIcon(Value As StdPicture)
        Set InternalBaseControlInfo.DragIcon = Value
    End Property

    [Serialize(False)]
    Public Property Let DragIcon(Value As StdPicture)
        Set InternalBaseControlInfo.DragIcon = Value
    End Property
End Class