#If FEATURE_FRAME And FEATURE_MULTIFRAME Then
[ClassId("73D277F8-39FC-4FA2-B941-670D2806DB4B")]
[InterfaceId("0774F775-F137-4C05-85D0-E0BECC4E5D80")]
[COMCreatable(False)]
[EventsUseDispInterface]
[ComImport(True)]
Private Class MultiFrameBaseCtl
    #Region "INHERITANCE"
     
        Inherits BaseControlWindowlessNoDrag
        
        [WithDispatchForwarding] Implements Control
        Implements IWindowsControl
        Implements IWindowElementEventsCommon
                
    #End Region
        
    #Region "STATE"
    
    Public Enum MultiFrameDirectionConstants
        vbDirectionHorizontal = 0
        vbDirectionVertical = 1
    End Enum
    
        [CustomDesigner("designer_SpectrumWindows")]
        [Description("")]
            Public BackColor As OLE_COLOR = VBRUN.SystemColorConstants.vbWindowBackground
            
            Public Direction As MultiFrameDirectionConstants = MultiFrameDirectionConstants.vbDirectionHorizontal
            
            Protected IsDesignMode As Boolean
            Protected InternalLastPaintWasAngled As Boolean
            
            Protected FramesCollection As Collection
                    
        [NonBrowsable]
        [Serialize(True)]
            Protected InternalSectionId As Integer = -1
                        
            Protected IsReportMode As Boolean
            Protected InternalIsShuffling As Boolean
    #End Region

    #Region "EVENTS"
                            
    [DefaultDesignerEvent]
    [Description("")]
            Event Initialize()

    #End Region
               
    #Region "MEMBERS"
        
        Sub New()
            BaseControlWindowlessNoDrag.New(ControlTypeConstants.vbShape)
        End Sub
    
        #If LOG_TERMINATE Then
            Protected Sub Class_Terminate()
                    Debug.Print CurrentComponentName & "." & CurrentProcedureName
            End Sub
        #End If
                
        Protected Sub HandleInitialize(ByVal ControlContext As ControlContext) _
                Implements IWindowsControl.Initialize
                
            Me.InternalStateResetRectDockable()     ' resets all the base class state()
            Me.InternalLastPaintWasAngled = False
            
            Dim SerializeInfo As Any = ControlContext.RuntimeUICtxGetSerializer()
            
            If Not SerializeInfo.RuntimeUISrzDeserialize(Me) Then
                'Caption_INIT = "Shape"
            End If
            IsDesignMode = SerializeInfo.RuntimeUISrzIsDesignMode()
            
            Dim InitData As WindowCreationData
            InitData.InternalSectionId = InternalSectionId + 1
            'InitData.Flags = IsContainer' Or RequiresWindowlessPaintingWithoutEvents
            InitData.WindowAtomIdx = CInt(EnumWindowAtoms.AtomIdx_ThunderPictureBox)
            'InitData.Caption = vbNullString
            'InitData.Flags = IsContainer
            CreateRootWindowElement(ControlContext, InitData)
            RootWindowElementBase.RuntimeUISetAndSinkEvents(Me)
            
            
            
            ' Dim styles As Any = CLng(ButtonStyles.BS_GROUPBOX)' + _
            '         If(_BaseControl.Appearance = AppearanceConstants.vbAppearFlat, ButtonStyles.BS_FLAT, 0&) + _
            '         If(Me.ClipControls = True, WS_CLIPCHILDREN, 0&)
            
            ' InitData.WindowAtomIdx = CInt(EnumWindowAtoms.AtomIdx_ThunderFrame)
            ' InitData.Caption = "Frame 0"
            ' InitData.WindowStyles = styles
            ' InitData.ExtendedStyles = If(ControlContext.IsPlacedOnUserControl, 0&, GeneralConsts.WindowExtendedStyles.WS_EX_NOPARENTNOTIFY) 'Or WS_EX_NOACTIVATE
            ' InitData.Flags = IsContainer Or _
            '                     ManualMouseCapture Or _
            '                     ForwardMouseDown Or _
            '                     ForwardMouseUp Or _
            '                     ForwardMouseMove Or _
            '                     ForwardButtonClick Or _
            '                     ForwardDragOver Or _
            '                     ForwardDoubleClick Or _
            '                     IgnoreWmCommandButtonClicks
            ' Set FrameWindowElements(0) = CreateRootWindowElementBase(ControlContext, InitData)
            'Debug.Print "here"
            
            
        End Sub
                
        Protected Sub HandleDestroy() _
                Implements IWindowsControl.Destroy
            #If LOG_TERMINATE Then
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            #End If
                
            ' disconnect anything that causes a circular reference here
            [_HiddenModule].ResetFirstMethodAccessFlag([_HiddenModule].GetInheritedOwner(Me))
        End Sub
        
        Protected Sub HandleCreate() _
                Implements IWindowElementEventsCommon.Create
                
            'Debug.Print CurrentComponentName & "." & CurrentProcedureName
            RaiseEvent Initialize()
                                    
            'Debug.Print CurrentComponentName & "." & CurrentProcedureName, 1
            
            Resize()
                        
            'Debug.Print CurrentComponentName & "." & CurrentProcedureName, 2
            RootWindowElementBase.RuntimeUIGetHandle().ShowWindow(SW_SHOW)
        End Sub

        [ArrayBoundsChecks(False)]
        Protected Sub HandlePaint(ByRef Handled As Boolean) _
                Implements IWindowElementEventsCommon.Paint
                                    
            Dim UnitPixelScale As Any = RootWindowElementBase.RuntimeUIGetUnitScale()
            Dim X As Long
            Dim Y As Long
            Dim Width As Long
            Dim Height As Long
            
            X = CLng(Me.PixelsLeft * UnitPixelScale)
            Y = CLng(Me.PixelsTop * UnitPixelScale)
            Width = CLng(Me.PixelsWidth * UnitPixelScale)
            Height = CLng(Me.PixelsHeight * UnitPixelScale)
            
            If Width <= 0 Or Height <= 0 Then Exit Sub
            
            Dim rect As tbRECT
            rect.Right = Width
            rect.Bottom = Height
            
            Dim backBrush As LongPtr
            Dim backColor As Long = TranslateColor(Me.BackColor)
            backBrush = CommonFillStyleToBrush(FillStyleConstants.vbFSTransparent, False, backColor, backColor)
            
            Dim ps As PAINTSTRUCT
            RootWindowElementBase.RuntimeUIBeginPaint(ps)    ' you MUST use this method, and NOT the BeginPaint API directly
    
                ps.hdc.FillRect(rect, backBrush)
                
            RootWindowElementBase.RuntimeUIEndPaint(ps)
            Handled = True     ' swallow up the event

            If backBrush <> 0 Then GDI32.DeleteObject(backBrush)

        End Sub

        [Serialize(False)]
        Public Property Get Parent() As Object ' As Form  FIXME, needs to work also for UCs
            Return ControlContext.RuntimeUICtxEnsureGetForm()
        End Property
        
        Protected Sub InternalChangedPositionOfFrame(ChangedFrame As FrameBaseCtl)
            'Debug.Print "InternalChangedPositionOfFrame"
            ' the new position is likely a conflict with an existing frame, so we need to shuffle the old positions around to accomodate the requested change
            ' First, remove the frame from the collection, and compact the MultiFramePositions that are higher than it
            If InternalIsShuffling = False Then
                InternalIsShuffling = True
                Dim currentFrame As Frame
                
                For Each currentFrame In FramesCollection
                    If currentFrame.MultiFramePosition > ChangedFrame.OriginalMultiFramePosition Then
                        If currentFrame IsNot ChangedFrame Then
                            'Debug.Print "adjusted position -1"
                            currentFrame.MultiFramePosition -= 1
                        End If
                    End If
                Next
                
                ' Next, make space for the new position
                For Each currentFrame In FramesCollection
                    If currentFrame.MultiFramePosition >= ChangedFrame.MultiFramePosition Then
                        If currentFrame IsNot ChangedFrame Then
                            'Debug.Print "adjusted position +1"
                            currentFrame.MultiFramePosition += 1
                        End If
                    End If
                Next
                InternalIsShuffling = False
            Else
                'Debug.Print "InternalChangedPositionOfFrame -> Already shuffling"
                Exit Sub
            End If
            
            'Debug.Print "InternalChangedPositionOfFrame [ DONE SHUFFLING ]"
            
            Set FramesCollection = Nothing      ' this will be resync'd
            Resize()
        End Sub
        
        Public Sub InternalChangedSizeOfFrame()
            Resize()
        End Sub
        
        Protected Sub Resize() _
                Implements IWindowElementEventsCommon.Resize
                                                  
            If FramesCollection Is Nothing Then
                'Debug.Print "Resize, FramesCollection Is NOTHING"
                Dim form As Form = CType(Of Form)(ControlContext.RuntimeUICtxEnsureGetForm())
                Set FramesCollection = New VBA.Collection
                Dim NewFrames As New VBA.Collection
                Dim formCtrl As Control
                Dim MaxMultiFramePosition As Long = -1
                
                For Each formCtrl In form
                    If TypeOf formCtrl Is Frame Then
                        'Debug.Print "control:", formCtrl.Name, Hex(formCtrl.hwnd)
                        If formCtrl.Container Is Me Then
                            'Debug.Print "Contained control:", formCtrl.Name, Hex(formCtrl.hwnd), formCtrl.MultiFramePosition
                            
                            If formCtrl.MultiFramePosition <> -1 Then
                                If FramesCollection.Exists("key" & CStr(formCtrl.MultiFramePosition)) Then
                                    NewFrames.Add(formCtrl)     ' conflict, so reset this key
                                Else
                                    FramesCollection.Add(formCtrl, "key" & formCtrl.MultiFramePosition)
                                    formCtrl.OriginalMultiFramePosition = formCtrl.MultiFramePosition
                                    
                                    If formCtrl.MultiFramePosition > MaxMultiFramePosition Then
                                        MaxMultiFramePosition = CLng(formCtrl.MultiFramePosition)
                                    End If
                                End If
                            Else
                                 NewFrames.Add(formCtrl)
                            End If
                        End If
                    End If
                Next
                
                If MaxMultiFramePosition > (FramesCollection.Count - 1) Then
                    Dim actualCount As Long = FramesCollection.Count
                    ' Something went wrong, we'll have to reset the MultiFramePositions completely to recover
                    'Debug.Print "OOPS! invalid MultiFramePositions, RESETTING (" & MaxMultiFramePosition & " vs " & FramesCollection.Count & ")"
                    'Set FramesCollection = New VBA.Collection
                    Set NewFrames = New VBA.Collection
                    
                    Dim ResetFramesCollection As VBA.Collection = New VBA.Collection
                    ' Try to keep the original order, e.g. when a frame gets deleted, but make them sequential
                    While ResetFramesCollection.Count < actualCount
                        Dim minPosition As Long = &H7FFFFFFF
                        Dim minControl As Frame = Nothing
                        For Each formCtrl In FramesCollection
                            If formCtrl.MultiFramePosition < minPosition Then
                                minPosition = CLng(formCtrl.MultiFramePosition)
                                Set minControl = CType(Of Frame)(formCtrl)
                            End If
                        Next
                        'Debug.Print "- resetting " & minControl.Name & " to " & ResetFramesCollection.Count
                        InternalIsShuffling = True
                        Dim oldKey As String = minControl.MultiFramePosition
                        minControl.MultiFramePosition = ResetFramesCollection.Count
                        InternalIsShuffling = False
                        minControl.OriginalMultiFramePosition = minControl.MultiFramePosition
                        FramesCollection.Remove("key" & oldKey)
                        ResetFramesCollection.Add(minControl, "key" & ResetFramesCollection.Count)
                    Wend
                    
                    Set FramesCollection = ResetFramesCollection
                End If
                
                'Debug.Print "FramesCollection.Count (before new) " & FramesCollection.Count
                
                For Each formCtrl In NewFrames
                    InternalIsShuffling = True
                    formCtrl.MultiFramePosition = FramesCollection.Count
                    InternalIsShuffling = False
                    formCtrl.OriginalMultiFramePosition = formCtrl.MultiFramePosition
                    FramesCollection.Add(formCtrl, "key" & FramesCollection.Count)
                Next
                'Debug.Print "FramesCollection.Count (after new) " & FramesCollection.Count
            End If
            
            With InternalBaseControlInfo
                
                'Debug.Print "FramesCollection.Count: ", FramesCollection.Count
                'Debug.Print "FramesCollection(key0): ", FramesCollection("key0").Name
                'Debug.Print "FramesCollection(key1): ", FramesCollection("key1").Name
                Dim FrameSizeTemp() As Long
                Dim framesCount As Long = FramesCollection.Count
                If framesCount > 0 Then
                    ReDim FrameSizeTemp(framesCount - 1) As Long
                End If
                Dim Frame As Frame
                Dim targetPos As Long = 0
                Dim countOfAutos As Long = 0
                Dim allocatedSize As Long
                
                
                For Each Frame In FramesCollection
                    If Frame.MultiFrameSize = 0 Then
                        ' split the remaining space equally to remaining frames, once we know what space is left
                        countOfAutos += 1
                    Else
                        If Direction = vbDirectionHorizontal Then
                            FrameSizeTemp(Frame.MultiFramePosition) = CLng((Frame.MultiFrameSize * RootWindowElementBase.RuntimeUIGetCurrentWidth()) / 100)
                        Else
                            FrameSizeTemp(Frame.MultiFramePosition) = CLng((Frame.MultiFrameSize * RootWindowElementBase.RuntimeUIGetCurrentHeight()) / 100)
                        End If
                        allocatedSize += FrameSizeTemp(Frame.MultiFramePosition)
                    End If
                Next
                   
                'Debug.Print "countOfAutos: ", countOfAutos
                'If countOfAutos > 0 Then
                    Dim unallocatedSpace As Long
                    If Direction = vbDirectionHorizontal Then
                        unallocatedSpace = RootWindowElementBase.RuntimeUIGetCurrentWidth() - allocatedSize
                    Else
                        unallocatedSpace = RootWindowElementBase.RuntimeUIGetCurrentHeight() - allocatedSize
                    End If
                    If unallocatedSpace > 0 Then
                        For Each Frame In FramesCollection
                            If Frame.MultiFrameSize = 0 Then
                                ' split the remaining space equally to remaining frames, once we know what space is left
                                FrameSizeTemp(Frame.MultiFramePosition) = CLng(unallocatedSpace \ countOfAutos)
                                allocatedSize += FrameSizeTemp(Frame.MultiFramePosition)
                                unallocatedSpace -= FrameSizeTemp(Frame.MultiFramePosition)
                                countOfAutos -= 1
                                'If countOfAutos = 0 Then Exit For                                
                            End If
                        Next
                    End If
                'End If
                
                For FrameIdx As Long = 0 To framesCount - 1
                    Set Frame = CType(Of Frame)(FramesCollection.Item("key" & FrameIdx))
                    'Debug.Print "**** MOVED FRAME " & Hex(Frame.hWnd)
                    
                    If Direction = vbDirectionHorizontal Then
                        UnprotectedAccess(Frame).InternalHWND.SetWindowPos(0, targetPos, 0, FrameSizeTemp(Frame.MultiFramePosition), RootWindowElementBase.RuntimeUIGetCurrentHeight(), 0)
                    Else
                        UnprotectedAccess(Frame).InternalHWND.SetWindowPos(0, 0, targetPos, RootWindowElementBase.RuntimeUIGetCurrentWidth(), FrameSizeTemp(Frame.MultiFramePosition), 0)
                    End If
                    targetPos += FrameSizeTemp(Frame.MultiFramePosition)
                Next
                                
            End With
        End Sub
        
        [Serialize(False)]
        Public Property Get hWnd() As LongPtr
            Return RootWindowElementBase.RuntimeUIGetHandle()
        End Property
        [Serialize(False)]
        Public Property Get Container() As Object
            On Error Resume Next
            Return RootWindowElementBase.RuntimeUIGetContainer()
        End Property
        
        Public Property Get FramesCount() As Long
            Return FramesCollection.Count
        End Property
        
        Public Property Let FramesCount(ByVal Value As Long)
            Set FramesCollection = Nothing      ' this will be resync'd
            Resize()
        End Property
        
        Protected Sub DirectionChanged() _
                Handles Direction.OnPropertyLet
        
            Resize()
        End Sub
                
        Protected Sub HandleLoad() _ 
                Implements IWindowElementEventsCommon.Load
            
            Resize()
        End Sub
    #End Region

End Class

[Description("A Win32 native Frame")]
[WindowsControl("/miscellaneous/divider.png")]
[ClassId("5724D906-E1EE-455E-9FE3-BB322399766F")]
[InterfaceId("00ABEAF7-B248-4E05-9A50-E69530F1DF16")]
[COMCreatable(False)]
[EventsUseDispInterface]
[ComImport(True)]
Class MultiFrame
    Inherits MultiFrameBaseCtl
    
    Protected Sub Class_BeforeFirstMethodAccess()
        [_HiddenModule].EnsureContainerIsLoaded(Me)
    End Sub
End Class
#End If