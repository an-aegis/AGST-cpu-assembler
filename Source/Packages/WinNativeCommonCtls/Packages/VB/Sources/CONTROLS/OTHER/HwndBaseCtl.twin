[ClassId("72CE1D0B-1011-45DD-8D0B-C7EEAA35F906")]
[InterfaceId("0FDEA31E-1904-4D90-9C93-49E7AC0DEE28")]
[COMCreatable(False)]
[ComImport(True)]
Class HwndBaseCtl
             
    #Region "INHERITANCE"
    
        Inherits BaseControlRectDockable
        
        [WithDispatchForwarding] Implements Control
        Implements IWindowsControl
        Implements IWindowElementEventsCommon

    #End Region
        
    #Region "STATE"
            Protected IsDesignMode As Boolean
    #End Region

    #Region "EVENTS"
    
    #End Region
               
    #Region "MEMBERS"

        Sub New()
            BaseControlRectDockable.New(ControlTypeConstants.vbHwndControl)
        End Sub
        
        Protected Sub CreateRootWindowElement(ByVal _ControlContext As ControlContext, _
                                                    ByRef InitData As WindowCreationData)
            InternalBaseControlBeforeCreateRootWindow(InitData)
            Me.ControlContext = _ControlContext
            InitData.WindowStyles += If(1, 0&, WS_DISABLED) + _
                                        If(1, WS_VISIBLE, 0&) + _
                                        If(1, WS_TABSTOP, 0&)
            RootWindowElementBase.Pointer = ControlContext.RuntimeUICtxCreateWindowElement(InitData)
            RootWindowElementBase.RuntimeUISetAndSinkEvents(Me)
        End Sub
        
        Protected Sub HandleInitialize(ByVal ControlContext As ControlContext) _
                Implements IWindowsControl.Initialize
            InternalStateResetRectDockable()     ' resets all the base class state()
            
            Dim SerializeInfo As Any = ControlContext.RuntimeUICtxGetSerializer()
            
            If Not SerializeInfo.RuntimeUISrzDeserialize(Me) Then
                'Caption_INIT = "Image"
            End If
            IsDesignMode = SerializeInfo.RuntimeUISrzIsDesignMode()
            
            If 1 Then       ' VTablePopulated(OnPaint)
                OnInitialize()
            End If
            
            Dim InitData As WindowCreationData
            InitData.Flags = ManualMouseCapture Or NeedsPrePostMessages
            InitData.WindowAtomIdx = CInt(EnumWindowAtoms.AtomIdx_ThunderGenericControl)
            CreateRootWindowElement(ControlContext, InitData)
            RootWindowElementBase.RuntimeUISetAndSinkEvents(Me)
        End Sub
                
        Protected Sub HandleDestroy() _
                Implements IWindowsControl.Destroy
            #If LOG_TERMINATE Then
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            #End If
                
            '[_HiddenModule].ResetFirstMethodAccessFlag([_HiddenModule].GetInheritedOwner(Me))
        End Sub
        
        Protected Sub HandleCreate() _
                Implements IWindowElementEventsCommon.Create
                
            'RaiseEvent Initialize()
        End Sub
                
        [Serialize(False)]
        Public Property Get Parent() As Object ' As Form  FIXME, needs to work also for UCs
            Return ControlContext.RuntimeUICtxEnsureGetForm()
        End Property
                
        Property Get hWnd() As HWND
            Return RootWindowElementBase.RuntimeUIGetHandle()
        End Property
        
        Protected Sub BeginPaint(wParam As LongPtr, ps As PAINTSTRUCT)
            If wParam Then
                Debug.Print "using wParam for HDC " & Hex(Me.hWnd)
                ps.hdc = wParam         ' if HDC provided in wParam, then use it
                Me.hWnd.GetClientRect(ps.rcPaint)
            Else
                Debug.Print "doing real BeginPaint() with HWND " & Hex(Me.hWnd)
                Me.hWnd.BeginPaint(ps)
            End If
        End Sub
        
        Protected Sub EndPaint(wParam As LongPtr, ps As PAINTSTRUCT)
            If wParam Then
                ' if HDC provided in wParam, then use it
            Else
                Me.hWnd.EndPaint(ps)
            End If
        End Sub
        
        ' offer a simple interface, Initialize (for getting styles, and perhaps a root window class), Paint, Resize, PreMessage, PostMessage
        
        Protected ClientRect As tbRECT
        Protected Sub SyncClientRect()
            Me.hWnd.GetClientRect(ClientRect)
        End Sub
        
        Sub PreProcessMessage(ByVal hwnd As LongPtr, ByVal Message As Long, ByVal wParam As LongPtr, ByVal lParam As LongPtr, MutedReturnValue As Variant, PostMessageCookie As Long) _
                Implements IWindowElementEventsCommon.PreProcessMessage

            If Message = WM_PAINT Then
                If 1 Then       ' VTablePopulated(OnPaint)
                    Dim ps As PAINTSTRUCT
                    BeginPaint(wParam, ps)
                        OnPaint(ps.hdc, ps.fErase <> 0, ps.rcPaint)
                    EndPaint(wParam, ps)
                    MutedReturnValue = 0
                    Exit Sub
                End If
            End If
                        
            If 1 Then       ' VTablePopulated(OnPreMessage)
                MutedReturnValue = OnMessageBefore(Message, Me.hWnd, wParam, lParam, PostMessageCookie)
            End If
        End Sub
        
        Private Sub PostProcessMessage(ByVal hwnd As LongPtr, ByVal Message As Long, ByVal wParam As LongPtr, ByVal lParam As LongPtr, returnValue As LongPtr, ByVal PostMessageCookie As Long) _
                Implements IWindowElementEventsCommon.PostProcessMessage
            
            If (Message = WM_SIZE) Or (Message = WM_MOVE) Then
                SyncClientRect()
                
                If 1 Then       ' VTablePopulated(OnResize)
                    OnResize()
                End If
            End If
            
            If 1 Then       ' VTablePopulated(OnPostMessage)
                OnMessageAfter(Message, Me.hWnd, wParam, lParam, PostMessageCookie, returnValue)
            End If
        End Sub

        ' ABSTRACT
        '[AllowUnpopulatedVtableEntry]
        Overridable Sub OnInitialize()
        End Sub
        
        ' ABSTRACT
        '[AllowUnpopulatedVtableEntry]
        Overridable Sub OnResize()
        End Sub
        
        ' ABSTRACT
        '[AllowUnpopulatedVtableEntry]
        Overridable Sub OnPaint(ByVal hdc As HDC, ByVal erase As Boolean, ByRef paintRect As tbRECT)
        End Sub
        
        ' ABSTRACT
        ' Occurs BEFORE the message is passed on to the default WNDPROC.  Returning a value will cause the message to be swallowed (i.e. not passed on to the default WNDPROC)
        '[AllowUnpopulatedVtableEntry]
        Overridable Function OnMessageBefore(ByVal Message As Long, ByVal hwnd As LongPtr, ByVal wParam As LongPtr, ByVal lParam As LongPtr, ByRef Cookie As Long) As Variant
        End Function
        
        ' ABSTRACT
        ' Occurs AFTER the message is passed on to the default WNDPROC
        '[AllowUnpopulatedVtableEntry]
        Overridable Sub OnMessageAfter(ByVal Message As Long, ByVal hwnd As LongPtr, ByVal wParam As LongPtr, ByVal lParam As LongPtr, ByRef Cookie As Long, ByRef ReturnValue As LongPtr)
        End Sub
                        
    #End Region
    
End Class

' [WindowsControl("/miscellaneous/ICONS??/Image??.png")]
' [ClassId("3DC4F3DB-B744-46DF-8846-2B0BDDFDE3EA")]   ' you must give your control a unique CLSID
' Private Class BlockColor
'     Inherits HwndControl
    
'     [CustomDesigner("designer_SpectrumWindows")]
'     Public BackColor As OLE_COLOR = vbRed
    
'     Sub OnPaint(ByVal hdc As LongPtr, ByVal erase As Boolean, ByRef paintRect As tbRECT) _
'             Overrides HwndControl.OnPaint
        
'         Dim brush As LongPtr = CreateSolidBrush(BackColor)
'         FillRect(hdc, paintRect, brush)
'     End Sub
    
'     Function OnPreMessage(ByVal Message As Long, ByVal hwnd As LongPtr, ByVal wParam As LongPtr, ByVal lParam As LongPtr, ByRef Cookie As Long) As Variant _
'             Overrides WindowedControl.OnPreMessage
            
'         Debug.Print "OnPreMessage ", Message
'     End Function
' End Class