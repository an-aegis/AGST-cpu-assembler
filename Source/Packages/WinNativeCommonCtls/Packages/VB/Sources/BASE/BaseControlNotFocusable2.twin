[ClassId("5D93AFFC-1494-417B-A383-3759B983E1CF")]
[InterfaceId("D66C7E51-3D21-48A8-8E9C-6F9A5872D7E1")]
[COMCreatable(False)]
Private Class BaseControlNotFocusable2

    Inherits BaseControlRectDockable
    Inherits BaseFont
    Implements IWindowElementEventsBaseControl
    
    ' like BaseControlFocusable, but without TabStop

    [Description("Adjusts the TAB key order of this control at runtime")]
    [Serialize(True, "TabIndex")]
        Protected TabIndexINIT As Long
    [Description("")]
        Public Appearance As VBRUN.AppearanceConstants = VBRUN.AppearanceConstants.vbAppear3d
    [Description("")]
    [Serialize(True, "MousePointer")]
        Protected MousePointerINIT As VBRUN.MousePointerConstants '= VBRUN.MousePointerConstants.vbDefault
    [Serialize(True, "MouseIcon")]
    [CustomDesigner("designer_IconBytes")]
        Protected ReadOnly MouseIconINIT() As Byte
        
    [CustomDesigner("designer_MultiLineText")]
    [Serialize(True, "ToolTipText")]
        Protected ToolTipTextINIT As String
        
    [Serialize(True, "VisualStyles")]
        Protected VisualStylesINIT As Boolean = True

    [Serialize(True, "DragIcon")]
    [CustomDesigner("designer_IconBytes")]
    [Description("")]
        Protected ReadOnly DragIconINIT() As Byte
    [Serialize(True, "DragMode")]
        Protected DragModeINIT As VBRUN.DragModeConstants
    
    [Description("Adjusts the visibility of this control at runtime")]
    [Serialize(True, ":Visible")]
        Protected VisibleINIT As Boolean = True
        [Description("")]
    [Serialize(True, "Enabled")]
        Protected EnabledINIT As Boolean = True
        
        Protected InternalWindowAtomCached As Integer
        
    Sub New(ByVal ControlType As ControlTypeConstants)
       BaseControlRectDockable.New(ControlType)
       BaseFont.New()
    End Sub
    
    Protected Sub InternalStateReset()
        InternalStateResetFont()
        InternalStateResetRectDockable()
    End Sub
            
    [Serialize(False)]
    [Description("")]
    Property Get Enabled() As Boolean
        Return CommonGetEnabled()
    End Property
    
    [Serialize(False)]
    [Description("")]
    Property Let Enabled(Value As Boolean)
        CommonLetEnabled(Value)
        Me.Refresh      ' Needed for Frame, otherwise the border can get drawn, even when BorderStyle is None
    End Property
    
    [Serialize(False)]
    [Description("Adjusts the visibility of this control at runtime")]
    Property Get Visible() As Boolean
        Return CommonGetVisible()
    End Property
    
    [Serialize(False)]
    [Description("Adjusts the visibility of this control at runtime")]
    Property Let Visible(Value As Boolean)
        CommonLetVisible(Value)
    End Property
    
    Protected Sub CreateRootWindowElement(ByVal _ControlContext As ControlContext, _
                                                ByRef InitData As WindowCreationData)
        InternalBaseControlBeforeCreateRootWindow(InitData)
        With CType(Of BaseControlInfo)(InitData.BaseControlInfoPtr)
            .ToolTipText = Me.ToolTipTextINIT
            .VisualStyles = Me.VisualStylesINIT
            .MousePointer = MousePointerINIT
            .DragMode = Me.DragModeINIT
            .HasTabIndex = True
            .TabIndex = Me.TabIndexINIT
        End With
        
        Me.InternalWindowAtomCached = InitData.WindowAtomIdx
        
        Me.ControlContext = _ControlContext
        InitData.WindowStyles += If(Me.EnabledINIT, 0&, WS_DISABLED) + _
                                 If(Me.VisibleINIT, WS_VISIBLE, 0&)
        RootWindowElementBase.Pointer = ControlContext.RuntimeUICtxCreateWindowElement(InitData)
        RootWindowElementBase.RuntimeUISetAndSinkEvents(Me)
        
        CommonLoadPictureInit(InternalBaseControlInfo.MouseIcon, Me.MouseIconINIT)
        CommonLoadPictureInit(InternalBaseControlInfo.DragIcon, Me.DragIconINIT)
    End Sub
    
    Protected Sub RecreateWindow(ByVal StyleFlags As Long)
        If Me.CurrentFontHandle <> 0 Then
            ' This happens due to calling RootWindowElement.RecreateWindow
            Me.CurrentFontHandle = 0        ' the handle was destroyed as it was selected into the old HWND
        End If
        RootWindowElementBase.RuntimeUIRecreateWindow(StyleFlags)
    End Sub
    
    Protected Sub HandleCreateOrDPIChange() _
            Implements IWindowElementEventsBaseControl.Create, _
                        IWindowElementEventsBaseControl.DPIChange
        SyncFontProperties()
        Me.Refresh
    End Sub
        
    Public Sub SetFocus()
        CommonSetFocus()
    End Sub
    
    Public Sub Refresh()
        CommonRefresh()
    End Sub
    
    [Serialize(False)]
    Public Property Get ToolTipText() As String
        Return InternalBaseControlInfo.ToolTipText
    End Property
    
    [Serialize(False)]
    Public Property Let ToolTipText(ByVal Value As String)
        InternalBaseControlInfo.ToolTipText = Value
        RootWindowElementBase.RuntimeUIToolTipChanged()
    End Property
    
    [Serialize(False)]
    Public Property Get VisualStyles() As Boolean
        Return InternalBaseControlInfo.VisualStyles
    End Property
    
    [Serialize(False)]
    [Description("Determines if the OS should use visual styles for rendering this control")]
    Public Property Let VisualStyles(ByVal Value As Boolean)
        Me.VisualStylesINIT = Value
        RootWindowElementBase.RuntimeUIRemoveVisualStyles(RootWindowElementBase.RuntimeUIGetHandle(), Not Value)
    End Property
        
    Public Sub Move(ByVal Left As Single, Optional ByVal Top As Variant, Optional ByVal Width As Variant, Optional ByVal Height As Variant)
        If InternalMove(Me, Left, Top, Width, Height) Then
            ControlContext.RuntimeUICtxChangedPosition()
        End If
    End Sub
    
    [Serialize(False)]
    [BindOnlyIfNoArguments]
    Public Property Get Left() As Double
        Return ControlContext.RuntimeUICtxGetScaledLeft()
    End Property
    
    [Serialize(False)]
    [BindOnlyIfNoArguments]
    Public Property Let Left(ByVal Value As Double)
        ControlContext.RuntimeUICtxSetScaledLeft(Value)
    End Property
    
    [Serialize(False)]
    Public Property Get Top() As Double
        Return ControlContext.RuntimeUICtxGetScaledTop()
    End Property
    
    [Serialize(False)]
    Public Property Let Top(ByVal Value As Double)
        ControlContext.RuntimeUICtxSetScaledTop(Value)
    End Property
    
    [Serialize(False)]
    Public Property Get Width() As Double
        Return ControlContext.RuntimeUICtxGetScaledWidth()
    End Property
    
    [Serialize(False)]
    Public Property Let Width(ByVal Value As Double)
        ControlContext.RuntimeUICtxSetScaledWidth(Value)
    End Property
    
    [Serialize(False)]
    Public Property Get Height() As Double
        Return ControlContext.RuntimeUICtxGetScaledHeight()
    End Property
    
    [Serialize(False)]
    Public Property Let Height(ByVal Value As Double)
        ControlContext.RuntimeUICtxSetScaledHeight(Value)
    End Property
    
    Public Sub Drag([TypeHint(DragConstants)] Optional ByVal Action As Variant)
        CommonDrag(Action)
    End Sub
    
    #If FEATURE_OLEDRAGDROP Then
    Protected Sub BaseSyncOLEDropMode(ByVal RootThis As Object, ByVal OLEDropMode As OLEDropConstants, ByRef DragDropHandler As OLEDragDropHandler, ByVal IsContainer As Boolean, ByVal AllowedAutomatic As Boolean)
        CommonSyncOLEDropMode(RootThis, OLEDropMode, DragDropHandler, ControlContext, RootWindowElementBase, IsContainer, AllowedAutomatic)
    End Sub
    #End If
    
    Public Sub ZOrder([TypeHint(ZOrderConstants)] Optional ByVal Position As Variant)
        CommonZOrder(Position, RootWindowElementBase.RuntimeUIGetHandle())
    End Sub

    [Serialize(False)]
    Public Property Get MouseIcon() As StdPicture
        Return InternalBaseControlInfo.MouseIcon
    End Property

    [Serialize(False)]
    Public Property Set MouseIcon(Value As StdPicture)
        Set InternalBaseControlInfo.MouseIcon = Value
        CommonMousePointerChanged()
    End Property

    [Serialize(False)]
    Public Property Let MouseIcon(Value As StdPicture)
        Set InternalBaseControlInfo.MouseIcon = Value
        CommonMousePointerChanged()
    End Property
    
    [Serialize(False)]
    Public Property Get hWnd() As LongPtr                           ' FIXME change to HWND once ALIAS is aligned
        Return CommonGetHWND()
    End Property

    Protected Property Get InternalHWND() As HWND
        Return CommonGetHWND()
    End Property
            
    [Serialize(False)]
    Public Property Get MousePointer() As MousePointerConstants
        Return InternalBaseControlInfo.MousePointer
    End Property
    
    [Serialize(False)]
    Public Property Let MousePointer(ByVal Value As MousePointerConstants)
        InternalBaseControlInfo.MousePointer = Value
        CommonMousePointerChanged()
    End Property
    
    [Serialize(False)]
    Public Property Get DragMode() As DragModeConstants
        Return InternalBaseControlInfo.DragMode
    End Property
    
    [Serialize(False)]
    Public Property Let DragMode(ByVal Value As DragModeConstants)
        InternalBaseControlInfo.DragMode = Value
        RootWindowElementBase.RuntimeUIDragModeChanged()
    End Property
    
    [Serialize(False)]
    Public Property Get TabIndex() As Long
        Return InternalBaseControlInfo.TabIndex
    End Property
    
    [Serialize(False)]
    Public Property Let TabIndex(ByVal Value As Long)
        InternalBaseControlInfo.TabIndex = Value
    End Property
    
    [Serialize(False)]
    Public Property Get DragIcon() As StdPicture
        Return InternalBaseControlInfo.DragIcon
    End Property

    [Serialize(False)]
    Public Property Set DragIcon(Value As StdPicture)
        Set InternalBaseControlInfo.DragIcon = Value
    End Property

    [Serialize(False)]
    Public Property Let DragIcon(Value As StdPicture)
        Set InternalBaseControlInfo.DragIcon = Value
    End Property
    
    Protected Function SendMessageLngPtr(ByVal msg As Long, ByVal wParam As LongPtr, ByVal lParam As LongPtr) As LongPtr
        Return RootWindowElementBase.RuntimeUIGetHandle().SendMessageW(msg, wParam, lParam)
    End Function
        
    Protected Function SendMessageLng(ByVal msg As Long, ByVal wParam As LongPtr, ByVal lParam As LongPtr) As Long
        Dim retValLngPtr As Any = RootWindowElementBase.RuntimeUIGetHandle().SendMessageW(msg, wParam, lParam)
        #If Win64 Then
            Dim retValLng As Long
            GetMem4(VarPtr(retValLngPtr), retValLng)
            Return retValLng
        #Else
            Return retValLngPtr
        #End If
    End Function
    
    Protected Sub AppearanceChanged() _  
            Handles Appearance.OnPropertyLet
                
        CommonSyncBorderStyle(ControlContext.RuntimeUICtxGetRootClassDispatch(), InternalWindowAtomCached)
    End Sub
End Class
