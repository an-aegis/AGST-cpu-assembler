Private Module FilePicker

    Public Type OPENFILENAMEW
        lStructSize         As Long
        hwndOwner           As LongPtr
        hInstance           As LongPtr
        lpstrFilter         As String
        lpstrCustomFilter   As String
        nMaxCustFilter      As Long
        nFilterIndex        As Long
        lpstrFile           As String
        nMaxFile            As Long
        lpstrFileTitle      As String
        nMaxFileTitle       As Long
        lpstrInitialDir     As String
        lpstrTitle          As String
        flags               As Long
        nFileOffset         As Integer
        nFileExtension      As Integer
        lpstrDefExt         As String
        lCustData           As LongPtr
        lpfnHook            As LongPtr
        lpTemplateName      As LongPtr
    End Type
    
    Public Const OFN_READONLY             As Long = &H1
    Public Const OFN_OVERWRITEPROMPT      As Long = &H2
    Public Const OFN_HIDEREADONLY         As Long = &H4
    Public Const OFN_NOCHANGEDIR          As Long = &H8
    Public Const OFN_SHOWHELP             As Long = &H10
    Public Const OFN_ENABLEHOOK           As Long = &H20
    Public Const OFN_ENABLETEMPLATE       As Long = &H40
    Public Const OFN_ENABLETEMPLATEHANDLE As Long = &H80
    Public Const OFN_NOVALIDATE           As Long = &H100
    Public Const OFN_ALLOWMULTISELECT     As Long = &H200
    Public Const OFN_EXTENSIONDIFFERENT   As Long = &H400
    Public Const OFN_PATHMUSTEXIST        As Long = &H800
    Public Const OFN_FILEMUSTEXIST        As Long = &H1000
    Public Const OFN_CREATEPROMPT         As Long = &H2000
    Public Const OFN_SHAREAWARE           As Long = &H4000
    Public Const OFN_NOREADONLYRETURN     As Long = &H8000&
    Public Const OFN_NOTESTFILECREATE     As Long = &H10000
    Public Const OFN_NONETWORKBUTTON      As Long = &H20000
    Public Const OFN_NOLONGNAMES          As Long = &H40000
    Public Const OFN_EXPLORER             As Long = &H80000
    Public Const OFN_NODEREFERENCELINKS   As Long = &H100000
    Public Const OFN_LONGNAMES            As Long = &H200000
    Public Const OFN_DONTADDTORECENT      As Long = &H2000000

    Private Const FILEBUF_CHARS As Long = 4096

    Public Function PickPictureFile( _
            ByVal ownerHwnd As LongPtr, _
            ByRef outPath As String, _
            Optional ByVal initialDir As String = vbNullString, _
            Optional ByVal titleText As String = "Browse picture", _
            Optional ByVal defaultExt As String = "png" _
        ) As Boolean

        Dim ofn     As OPENFILENAMEW
        Dim sFilter As String
        sFilter = BuildPictureFilter()

        Dim sFile32 As String
        sFile32 = String$(FILEBUF_CHARS - 1, vbNullChar)

        ofn.lStructSize = LenB(ofn)
        ofn.hwndOwner = ownerHwnd
        ofn.lpstrFilter = sFilter
        ofn.nFilterIndex = 1
        ofn.lpstrFile = sFile32
        ofn.nMaxFile = FILEBUF_CHARS
        ofn.lpstrFileTitle = vbNullString
        ofn.nMaxFileTitle = 0
        ofn.lpstrInitialDir = initialDir
        ofn.lpstrTitle = titleText
        ofn.flags = OFN_EXPLORER Or OFN_HIDEREADONLY Or OFN_FILEMUSTEXIST Or OFN_PATHMUSTEXIST Or OFN_NOCHANGEDIR
        ofn.lpstrDefExt = defaultExt

        Dim res As Long = COMDLG32.GetOpenFileNameW(ofn)
        If res <> 0 Then
            outPath = Left$(ofn.lpstrFile, InStr$(1, ofn.lpstrFile, vbNullChar) - 1)
            Return True
        End If
        Return False
    End Function

    Private Function BuildPictureFilter() As String
        Dim allPics As String
        allPics = "*.bmp;*.dib;*.ico;*.cur;*.wmf;*.emf;*.gif;*.jpg;*.png"

        ' Each pair is: DisplayText & vbNullChar & Pattern & vbNullChar
        Dim s As String
        s = "All Picture Files" & vbNullChar & allPics & vbNullChar & _
            "Bitmaps (*.bmp, *.dib)" & vbNullChar & "*.bmp;*.dib" & vbNullChar & _
            "Icons & Cursors (*.ico, *.cur)" & vbNullChar & "*.ico;*.cur" & vbNullChar & _
            "Metafiles (*.wmf, *.emf)" & vbNullChar & "*.wmf;*.emf" & vbNullChar & _
            "GIF Images (*.gif)" & vbNullChar & "*.gif" & vbNullChar & _
            "JPEG Images (*.jpg)" & vbNullChar & "*.jpg" & vbNullChar & _
            "PNG Images (*.png)" & vbNullChar & "*.png" & vbNullChar & _
            "All files (*.*)" & vbNullChar & "*.*" & vbNullChar & vbNullChar

        BuildPictureFilter = s
    End Function
    
End Module