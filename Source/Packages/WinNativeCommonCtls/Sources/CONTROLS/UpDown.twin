#If FEATURE_UPDOWN Then
[ClassId("8C529525-EBC9-4B14-8C35-842B1FD11CE5")]
[InterfaceId("63719637-EC85-41EC-ACE8-445226F29D59")]
[COMCreatable(False)]
[EventsUseDispInterface]
Class UpDownBaseCtl
	
	#Region "INHERITANCE"

        Inherits VB.BaseControlFocusableNoFont
        
        [WithDispatchForwarding] Implements Control
        Implements VB.IWindowsControl
        Implements VB.IWindowElementEventsCommon
        Implements VB.IWindowElementEventsUC
        Implements VB.IWindowElementEventsAX
        Implements VB.IWindowElementEventsCommonControls
        
    #End Region
            
    #Region "STATE"
        #If FEATURE_OLEDRAGDROP Then
        [CustomDesigner("designer_RestrictedOLEDropMode")]
            Public OLEDropMode As VBRUN.OLEDropConstants
            
        Protected OLEDragDropHandler As VB.OLEDragDropHandler
        #End If
        
        [CustomDesigner("designer_MultiLineText")]
        [Serialize(True, "ToolTipText")]
            Protected ToolTipTextINIT As String
        
        [Serialize(True, "Min")]
            Protected Min_INIT As Long = 0
        [Serialize(True, "Max")]
            Protected Max_INIT As Long = 10
        [Serialize(True, "Value")]
            Protected Value_INIT As Long = 0
        [Serialize(True, "Increment")]
            Protected Increment_INIT As Long = 1
            
            Protected IsInitialized As Boolean
        
            Public Orientation As OrientationConstants = OrientationConstants.ccOrientationHorizontal

        [Serialize(True, "VisualStyles")]
            Protected VisualStylesINIT As Boolean = True
                            
        [Description("Opacity, given as a percentage, 0 - 100.  REQUIRES TARGET OS 6.2+ FOR CHILD CONTROLS.")]
            Public Opacity As Double = 100
        
        [CustomDesigner("designer_SpectrumWindowsOrClear")]
        [Description("A color, when set, that will appear fully transparent in the window.  REQUIRES TARGET OS 6.2+ FOR CHILD CONTROLS.")]
            Public TransparencyKey As OLE_COLOR = -1
        
            Protected LastClickWasDownwards As Boolean
            Protected IsDesignMode As Boolean = False
        
        Private Type UDACCEL
            nSec As Long
            nInc As Long
        End Type
        
        Private Type NMUPDOWN
            hdr As NMHDR
            iPos As Long
            iDelta As Long
        End Type
    #End Region

    #Region "EVENTS"
    
        [DefaultDesignerEvent]
        [Description("")]
            Event Change()
        [Description("")]
            Event UpClick()
        [Description("")]
            Event DownClick()
        [Description("")]
            Event GotFocus()
        [Description("")]
            Event LostFocus()
        [Description("")]
            Event MouseDown(Button As Integer, Shift As Integer, X As Single, Y As Single)
        [Description("")]
            Event MouseMove(Button As Integer, Shift As Integer, X As Single, Y As Single)
        [Description("")]
            Event MouseUp(Button As Integer, Shift As Integer, X As Single, Y As Single)
        [Description("")]
            Event Initialize()
        [Description("")]
            Event DragDrop(Source As Control, X As Single, Y As Single)
        [Description("")]
            Event DragOver(Source As Control, X As Single, Y As Single, State As Integer)
        Event OLECompleteDrag(Effect As Long)
        Event OLEDragDrop(Data As DataObject, Effect As Long, Button As Integer, Shift As Integer, X As Single, Y As Single)
        Event OLEDragOver(Data As DataObject, Effect As Long, Button As Integer, Shift As Integer, X As Single, Y As Single, State As Integer)
        Event OLEGiveFeedback(Effect As Long, DefaultCursors As Boolean)
        Event OLESetData(Data As DataObject, DataFormat As Integer)
        Event OLEStartDrag(Data As DataObject, AllowedEffects As Long)
        Event Validate(Cancel As Boolean)
        
    #End Region
               
    #Region "MEMBERS"
        Sub New()
            BaseControlFocusableNoFont.New(ControlTypeConstants.vbUpDown)
        End Sub
        
        Protected Sub HandleLoad() _
                Implements VB.IWindowElementEventsCommon.Load
            
            Dim Opacity As Any = Me.Opacity
            If ((Opacity >= 0) And (Opacity < 100)) Or (TransparencyKey <> -1) Then
                SyncOpacity
            End If
        End Sub
        
        Protected Sub SyncOpacity() _ 
                Handles Opacity.OnPropertyLet, _
                        TransparencyKey.OnPropertyLet
            
            RootWindowElementBase.RuntimeUIGetHandle().CommonSyncOpacity(Me.TransparencyKey, Me.Opacity)
        End Sub
        
        Protected Sub HandleInitialize(ByVal ControlContext As VB.ControlContext) _
                Implements VB.IWindowsControl.Initialize
            
            Me.InternalStateResetRectDockable()     ' resets all the base class state
            Me.IsInitialized = False
            Me.LastClickWasDownwards = False
            
            Dim SerializeInfo As Any = ControlContext.RuntimeUICtxGetSerializer()
            
            If Not SerializeInfo.RuntimeUISrzDeserialize(Me) Then
                'Debug.Print "UpDown.HandleInitialize -- NEW CONTROL(" & Me.PixelsHeightINIT & "," & Me.PixelsWidthINIT & ")"
                If SerializeInfo.RuntimeUISrzGetOrientationHint() Then
                    Me.Orientation = OrientationConstants.ccOrientationVertical
                End If
            End If
            IsDesignMode = SerializeInfo.RuntimeUISrzIsDesignMode()
            
            With InternalBaseControlInfo
                .NamePtr = VarPtr(Me.NameINIT)
                .ToolTipText = Me.ToolTipTextINIT
                .VisualStyles = Me.VisualStylesINIT
            End With
                        
            Dim Opacity As Any = Me.Opacity
            If Opacity > 100 Then Me.Opacity = 100
            If Opacity < 0 Then Me.Opacity = 0
            
            Dim InitData As VB.WindowCreationData
            InitData.WindowAtomIdx = CInt(VB.EnumWindowAtoms.AtomIdx_ThunderUpDown)
            InitData.WindowStyles = GetStyles()
            InitData.SubClass = True
            CreateRootWindowElement(ControlContext, InitData)
            RootWindowElementBase.RuntimeUISetAndSinkEvents(Me)
        End Sub
        
        Protected Function GetStyles() As Long
            Dim Styles As Long
        	Styles += If (Orientation = OrientationConstants.ccOrientationHorizontal, UDS_HORZ, 0&)
            Return Styles
        End Function
                
        Protected Sub HandleDestroy() _
                Implements VB.IWindowsControl.Destroy
            #If LOG_TERMINATE Then
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            #End If
            
            ' disconnect anything that causes a circular reference here
            #If FEATURE_OLEDRAGDROP Then
            If OLEDragDropHandler IsNot Nothing Then OLEDragDropHandler.Disconnect()
            #End If
            
            [_HiddenModule].ResetFirstMethodAccessFlag([_HiddenModule].GetInheritedOwner(Me))
        End Sub

        #If LOG_TERMINATE Then
            Protected Sub Class_Terminate()
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            End Sub
        #End If
        		
        Protected Sub HandleCreate() _
                Implements VB.IWindowElementEventsCommon.Create

            ' NOTE: changing the property assignments here often means changes to SyncRecreate() too
            Me.Min = Min_INIT
            Me.Max = Max_INIT
            Me.Value = Value_INIT
            Me.Increment = Increment_INIT

            ' The OS will auto-shrink this control, so we have to 'update' the dimensions manually
            Dim widthPixels As Long = CLng(RootWindowElementBase.RuntimeUIScaleX(Me.PixelsWidth, vbScaledPixels, vbPixels))
            Dim heightPixels As Long = CLng(RootWindowElementBase.RuntimeUIScaleY(Me.PixelsHeight, vbScaledPixels, vbPixels))
            Dim hwnd As Any = RootWindowElementBase.RuntimeUIGetHandle()
            hwnd.SetWindowPos(0, 0, 0, widthPixels, heightPixels, VB.SWP_NOZORDER Or VB.SWP_NOACTIVATE Or VB.SWP_NOMOVE Or VB.SWP_NOOWNERZORDER)
            
            #If FEATURE_OLEDRAGDROP Then
            SyncOLEDropMode ()
            #End If
            RaiseEvent Initialize()
            
            IsInitialized = True
        End Sub
        
        Protected Sub SyncRecreate() _
                Handles Orientation.OnPropertyLet
                        
            Dim Min As Long = Me.Min
            Dim Max As Long = Me.Max
            Dim Value As Long = Me.Value
            Dim Increment As Long = Me.Increment
            
            ' changing these flags in the GWL_STYLE has no effect at runtime, so we have to recreate the control
            RecreateWindow(GetStyles())
            
            Me.Min = Min
            Me.Max = Max
            Me.Value = Value
            Me.Increment = Increment

        End Sub
        
        Protected Sub HandleMouseDown(ByVal Button As VBRUN.MouseButtonConstants, _
                                        ByVal ShiftState As VBRUN.ShiftConstants, _
                                        ByVal X As Single, ByVal Y As Single, ByRef SwallowMessage As Boolean) _
                Implements VB.IWindowElementEventsCommon.PreMouseDown
            
            RaiseEvent MouseDown(CInt(Button), CInt(ShiftState), X, Y)
        End Sub
        
        Protected Sub HandleMouseMove(ByVal Button As VBRUN.MouseButtonConstants, _
                                        ByVal ShiftState As VBRUN.ShiftConstants, _
                                        ByVal X As Single, ByVal Y As Single) _
                Implements VB.IWindowElementEventsCommon.MouseMove
                
            RaiseEvent MouseMove(CInt(Button), CInt(ShiftState), X, Y)
        End Sub
        
        Protected Sub HandleMouseUp(ByVal Button As VBRUN.MouseButtonConstants, _
                                    ByVal ShiftState As VBRUN.ShiftConstants, _
                                    ByVal X As Single, ByVal Y As Single, ByRef SwallowMessage As Boolean) _
                Implements VB.IWindowElementEventsCommon.PreMouseUp
                
            RaiseEvent MouseUp(CInt(Button), CInt(ShiftState), X, Y)
        End Sub
        
        Protected Sub HandleGotFocus() _
                Implements VB.IWindowElementEventsUC.GotFocus
                
            RaiseEvent GotFocus()
        End Sub

        Protected Sub HandleLostFocus() _
                Implements VB.IWindowElementEventsUC.LostFocus
                
            RaiseEvent LostFocus()
        End Sub
        
        [Serialize(False)]
        Public Property Get Min() As Long
            Dim MinValue As Long
            Dim MaxValue As Long
            SendMessageLngPtr(UDM_GETRANGE32, VarPtr(MinValue), VarPtr(MaxValue))
            Return MinValue
        End Property
        
        [Serialize(False)]
        Public Property Let Min(ByVal Value As Long)
            SendMessageLngPtr(UDM_SETRANGE32, Value, Me.Max)
        End Property
        
        [Serialize(False)]
        Public Property Get Max() As Long
            Dim MinValue As Long
            Dim MaxValue As Long
            SendMessageLngPtr(UDM_GETRANGE32, VarPtr(MinValue), VarPtr(MaxValue))
            Return MaxValue
        End Property
        
        [Serialize(False)]
        Public Property Let Max(ByVal Value As Long)
            SendMessageLngPtr(UDM_SETRANGE32, Me.Min, Value)
        End Property
                
        [Serialize(False), DefaultMember]
        Public Property Get Value() As Long
            Value = SendMessageLng(UDM_GETPOS32, 0, 0)
        End Property
        
        [Serialize(False), DefaultMember]
        Public Property Let Value(ByVal NewValue As Long)
            If Me.Value <> NewValue Then
            SendMessageLngPtr(UDM_SETPOS32, 0, NewValue)
                If IsInitialized Then RaiseEvent Change
            End If
        End Property
        
        [Serialize(False)]
        Public Property Get Parent() As Object ' As Form  FIXME, needs to work also for UCs
            Return ControlContext.RuntimeUICtxEnsureGetForm()
        End Property
        
        [Serialize(False)]
        Public Property Get Object() As Object
            Return Me
        End Property

        Protected Sub HandleDragOver(ByVal Source As Object, ByVal X As Double, ByVal Y As Double, ByVal State As Long) _
                Implements VB.IWindowElementEventsAX.DragOver
            
            If State = 3 Then
                RaiseEvent DragDrop(Source, CSng(X), CSng(Y))
            Else
                RaiseEvent DragOver(Source, CSng(X), CSng(Y), CInt(State))
            End If
        End Sub
                
        [Serialize(False)]
        Public Property Get Increment() As Long
            Dim Accel As UDACCEL
            SendMessageLngPtr(UDM_GETACCEL, 1, VarPtr(Accel))
            Return Accel.nInc
        End Property
        
        [Serialize(False)]
        Public Property Let Increment(ByVal Value As Long)
            Dim Accel As UDACCEL
            Accel.nSec = 0
            Accel.nInc = Value
            SendMessageLngPtr(UDM_SETACCEL, 1, VarPtr(Accel))
        End Property
        
        #If FEATURE_OLEDRAGDROP Then
        Public Sub OLEDrag()
            VB.CommonOLEDrag(Me)
        End Sub

        Protected Sub SyncOLEDropMode() _
                Handles OLEDropMode.OnPropertyLet
                
            BaseSyncOLEDropMode (Me, Me.OLEDropMode, Me.OLEDragDropHandler, False, True)
        End Sub
        #End If
        
        Protected Sub HandleUpDownNotification(ByRef Notification As NMUPDOWN, ByRef MutedReturnValue As Variant)
            Select Case Notification.iDelta
                Case 0
                    MutedReturnValue = 1
                Case Is < 0
                    LastClickWasDownwards = True
                Case Is > 0
                    LastClickWasDownwards = False
            End Select
        End Sub
        
        Protected Sub Notify(ByRef Notification As NMHDR, MutedReturnValue As Variant) _
                Implements VB.IWindowElementEventsCommonControls.Notify
            
            Const UDN_FIRST As Long = (-721)
            Const UDN_DELTAPOS As Long = (UDN_FIRST - 1)
            'Debug.Print "Notify: " & Notification.Code
            ' Despite docs, NM_RELEASEDCAPTURE doesn't get notified.
            If Notification.Code = UDN_DELTAPOS Then
                HandleUpDownNotification(ByVal VarPtr(Notification), MutedReturnValue)
            ' ElseIf Notification.Code = NM_RELEASEDCAPTURE Then
            '     HandleUpDownReleasedCapture(VarPtr(Notification), MutedReturnValue)
            End If
        End Sub
        
        Protected Sub Scroll(ByVal ScrollType As Long, ByVal IsHorizontal As Boolean) _
                Implements VB.IWindowElementEventsCommon.Scroll
            
            If ScrollType <> TB_ENDTRACK Then
               	RaiseEvent Change
            Else
                If LastClickWasDownwards = True Then
                    RaiseEvent DownClick
                Else
                    RaiseEvent UpClick
                End If
            End If
        End Sub
        
        Protected Sub HandleValidate(Cancel As Boolean) _
                Implements VB.IWindowElementEventsCommon.Validate

            RaiseEvent Validate(Cancel)
        End Sub
                
        [Serialize(False)]
        Public Property Get ToolTipText() As String
            Return InternalBaseControlInfo.ToolTipText
        End Property
    
        [Serialize(False)]
        Public Property Let ToolTipText(ByVal Value As String)
            InternalBaseControlInfo.ToolTipText = Value
            RootWindowElementBase.RuntimeUIToolTipChanged()
        End Property
        
        [Serialize(False)]
        [Description("Determines if the OS should use visual styles for rendering this control")]
        Public Property Get VisualStyles() As Boolean
            Return InternalBaseControlInfo.VisualStyles
        End Property
    
        [Serialize(False)]
        [Description("Determines if the OS should use visual styles for rendering this control")]
        Public Property Let VisualStyles(ByVal Value As Boolean)
            Me.VisualStylesINIT = Value
            RootWindowElementBase.RuntimeUIRemoveVisualStyles(RootWindowElementBase.RuntimeUIGetHandle(), Not Value)
        End Property
    #End Region
	
End Class

[WindowsControl("/miscellaneous/ICONS??/UpDown??.png")]
[ClassId("98E78A9F-2720-4A7F-A320-905436924746")]
[InterfaceId("AC9BAD81-6AE9-4ADE-93F2-A5369C2AD729")]
[COMCreatable(False)]
[EventsUseDispInterface]
Class UpDown
    Inherits UpDownBaseCtl
    
    Protected Sub Class_BeforeFirstMethodAccess()
        [_HiddenModule].EnsureContainerIsLoaded(Me)
    End Sub
End Class
#End If