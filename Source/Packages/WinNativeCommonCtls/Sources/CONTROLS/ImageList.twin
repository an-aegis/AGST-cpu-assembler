#If FEATURE_IMAGELIST Then
[InterfaceId("A68E98AD-5ABA-4D1B-9926-718ADFF62B89")]
[ComImport(True)]
Interface TbImageListPrivate Extends stdole.IUnknown
    Function GetScaleX() As Double
    Function GetScaleY() As Double
    Sub SetWidthHeight(ByVal width As Long, ByVal height As Long)
    Sub SyncListProps()
    Sub GetInitImageData(Images() As String, Keys() As String, Tags() As String)
    Sub SetInitImageData(Images() As String, Keys() As String, Tags() As String)
    Property Get ImageWidth() As Long
    Property Let ImageWidth(ByVal Value As Long)
    Property Get ImageHeight() As Long
    Property Let ImageHeight(ByVal Value As Long)
    Property Get ColorDepth() As ImageListColorDepth
    Property Get UseMaskColor() As Boolean
    Property Get MaskColor() As OLE_COLOR
    Property Get BoundCount() As Long
End Interface

[ClassId("3474B17E-2CB0-427C-8E48-A54AFB24A9B4")]
[InterfaceId("5FB4707A-74F6-455A-93CF-48A7991D49F2")]
[EventInterfaceId("7E4E64BD-BA49-4F40-9330-F20E02EA2D2A")]
[COMCreatable(False)]
[EventsUseDispInterface]
Class ImageListBaseCtl
     
    #Region "INHERITANCE"

        Inherits VB.BaseControlNotFocusable
        
        [WithDispatchForwarding] Implements Control
        Implements VB.IWindowsControl
        Implements VB.ITwinBasicDesignerExtensions
        Implements TbImageListPrivate
        
    #End Region
            
    #Region "STATE"
            
        Enum ImageListColorDepth
            ColorDepth4Bit = &H4
            ColorDepth8Bit = &H8
            ColorDepth16Bit = &H10
            ColorDepth24Bit = &H18
            ColorDepth32Bit = &H20
        End Enum
        
        [CustomDesigner("designer_SpectrumWindows")]
        Public BackColor As OLE_COLOR = vbWindowBackground
        Public UseBackColor As Boolean = False
        
        [CustomDesigner("designer_SpectrumWindows")]
        Public MaskColor As OLE_COLOR = &H00C0C0C0
        Public UseMaskColor As Boolean = True
                
        [Serialize(True, ":ImageHeight")]
        Protected ImageHeight_INIT As Long
        [Serialize(True, ":ImageWidth")]
        Protected ImageWidth_INIT As Long
        
        Public ColorDepth As ImageListColorDepth = ImageListColorDepth.ColorDepth32Bit

        [Serialize(True)]
        [NonBrowsable(True)]
        [Hidden]
        [CustomDesigner("BINARY")]      ' ensures that binary data is persisted for the serialized string, rather than just UTF8
        Protected InternalImages_INIT() As String
        [Serialize(True)]
        [NonBrowsable(True)]
        [Hidden]
        Protected InternalKeys_INIT() As String
        [Serialize(True)]
        [NonBrowsable(True)]
        [Hidden]
        Protected InternalTags_INIT() As String
                
        [Serialize(False)]
        Protected InternalListImages As ListImages
        
        [Serialize(False)]
        [Hidden]
        Protected BoundCount As Long
        Protected IsDesignMode As Boolean
        Protected InternalImageWidth As Long
        Protected InternalImageHeight As Long
                
    #End Region

    #Region "EVENTS"

    #End Region
               
    #Region "MEMBERS"
    
        Sub New()
            BaseControlNotFocusable.New(ControlTypeConstants.vbImageList)
        End Sub
    
        Protected Sub HandleGetPropertyExtensions(ByRef out() As String) _
                Implements VB.ITwinBasicDesignerExtensions.GetPropertyExtensions

            ReDim out(0)
            out(0) = "Custom"
        End Sub

        Protected Sub HandleInvokePropertyExtension(ByVal Name As String, ByRef MadeChanges As Boolean) _
                Implements VB.ITwinBasicDesignerExtensions.InvokePropertyExtension

            If Name = "Custom" Then
                MadeChanges = ShowImageListPropertyPage(Me)
            End If
        End Sub
        
        Protected Sub LoadFromPersistedState()
            'Stop
            Me.InternalImageWidth = Me.ImageWidth_INIT
            Me.InternalImageHeight = Me.ImageHeight_INIT
            If (Me.IsDesignMode = False) And IsArrayInitialized(InternalImages_INIT) Then
                Dim picIndex As Long
                Dim expectedUbound As Long = UBound(InternalImages_INIT)
                On Error GoTo DoRedimKeys
                If UBound(InternalKeys_INIT) <> expectedUbound Then
                    DoRedimKeys:
                    ReDim Preserve InternalKeys_INIT(expectedUbound)    ' handles mismatch
                End If
                On Error GoTo DoRedimTags
                If UBound(InternalTags_INIT) <> expectedUbound Then
                    DoRedimTags:
                    ReDim Preserve InternalTags_INIT(expectedUbound)    ' handles mismatch
                End If
                On Error GoTo 0
                Dim picStr As String
                Dim bArray() As Byte
                Dim pic As StdPicture
                For Each picStr In InternalImages_INIT
                    On Error Resume Next
                    bArray = picStr
                    Set pic = Nothing
                    Set pic = Global.LoadPicture(bArray)
                    
                    If pic Is Nothing Then
                        ' Rather than balking on bad files here, we allow it through but with a substituted picture
                        ' This prevents the problem of the image list not working if the current OS doesn't support the file format (e.g. Win7 not supporting 32-bit ICO)
                        Const ImageData_Exclamation As Variant = LoadResDataInternal("Exclamation.gif", "OTHER")
                        Set pic = Global.LoadPicture(ImageData_Exclamation)
                    End If
                    
                    InternalListImages.Add(, InternalKeys_INIT(picIndex), pic, InternalTags_INIT(picIndex))
                    Erase bArray
                    picIndex += 1
                Next
            End If
        End Sub
        
        Protected Sub HandleInitialize(ByVal ControlContext As VB.ControlContext) _
                Implements VB.IWindowsControl.Initialize
            
            Me.InternalStateResetRect()     ' resets all the base class state
            BoundCount = 0
            InternalImageWidth = 0
            InternalImageHeight = 0
            
            Dim SerializeInfo As Any = ControlContext.RuntimeUICtxGetSerializer()
            If Not SerializeInfo.RuntimeUISrzDeserialize(Me) Then
                ' set defaults here
            End If
            Set InternalListImages = New ListImages
            InternalListImages.OwnerWEAK = ObjPtr(Me)
            IsDesignMode = SerializeInfo.RuntimeUISrzIsDesignMode()
            
            Me.Initialize(ControlContext)
            
            Dim InitData As VB.WindowCreationData
            InternalBaseControlBeforeCreateRootWindow(InitData)
            
            ControlContext.RuntimeUICtxSetBaseControlInfoPtr(VarPtr(InternalBaseControlInfo))
            ControlContext.RuntimeUICtxSetControlArrayIndex(InternalBaseControlInfo.ControlArrayIndex)
            
            LoadFromPersistedState()
        End Sub
                
        Protected Sub HandleDestroy() _
                Implements VB.IWindowsControl.Destroy
            #If LOG_TERMINATE Then
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            #End If
            
            ' disconnect anything that causes a circular reference here
            'Debug.Print "Destroying image list " & InternalListImages.hImageList
            COMCTL32.ImageList_Destroy(InternalListImages.hImageList)
            Set InternalListImages = Nothing
            
            [_HiddenModule].ResetFirstMethodAccessFlag([_HiddenModule].GetInheritedOwner(Me))
        End Sub

        #If LOG_TERMINATE Then
            Protected Sub Class_Terminate()
                Debug.Print CurrentComponentName & "." & CurrentProcedureName
            End Sub
        #End If
                        
        [Serialize(False)]
        Public Property Get Parent() As Object ' As Form  FIXME, needs to work also for UCs
            Return ControlContext.RuntimeUICtxEnsureGetForm()
        End Property
        
        [Serialize(False)]
        Public Property Get Object() As Object
            Return Me
        End Property
        
        Protected Sub HandleDesignerClick(ByVal X As Long, ByVal Y As Long) _
            Implements VB.ITwinBasicDesignerExtensions.DesignerClick
        End Sub
        
        Protected Sub HandleDesignTimePrepareSnapshot() _
            Implements VB.ITwinBasicDesignerExtensions.DesignTimePrepareSnapshot
        End Sub
        
        Protected Function HandleGetClientObject() As stdole.IUnknown _
            Implements VB.ITwinBasicDesignerExtensions.GetClientObject
        End Function
        
        Protected Sub HandleGetIViewObject(out As stdole.IUnknown, outRect As VB.tbRECT, outIsVisible As Boolean, isUcInsideActiveProject As Boolean) _
            Implements VB.ITwinBasicDesignerExtensions.GetIViewObject
        End Sub
        
        [Serialize(False)]
        Public Property Get hImageList() As LongPtr
            Return InternalListImages.hImageList
        End Property
        
        Public Function Overlay(Key1 As Variant, Key2 As Variant) As StdPicture
            'Dim imageList As LongPtr = Me.InternalListImages.hImageList
            Dim tempImageList As LongPtr
            tempImageList = COMCTL32.ImageList_Create(InternalImageWidth, InternalImageHeight, ILC_MASK Or ILC_COLOR24, 4, 4)
            Dim icon1 As StdPicture = Me.InternalListImages(Key1).ExtractIcon()
            Dim icon2 As StdPicture = Me.InternalListImages(Key2).ExtractIcon()
            COMCTL32.ImageList_AddIcon tempImageList, icon1.Handle
            COMCTL32.ImageList_AddIcon tempImageList, icon2.Handle
            COMCTL32.ImageList_SetOverlayImage(tempImageList, 1, 1)
            Dim iconHandle As LongPtr = COMCTL32.ImageList_GetIcon(tempImageList, 0, ILD_TRANSPARENT Or &H100)
            COMCTL32.ImageList_Destroy(tempImageList)
            Return CType(Of StdPicture)(CreateStdPictureFromHandle(iconHandle, vbPicTypeIcon, True))
        End Function
                
        Protected Sub SyncListProps() _
                Handles BackColor.OnPropertyLet, _
                        UseBackColor.OnPropertyLet
    
            If InternalListImages.hImageList <> vbNullPtr Then
                COMCTL32.ImageList_SetBkColor InternalListImages.hImageList, If(UseBackColor, TranslateColor(BackColor), -1&)
            End If
        End Sub
        
        Protected Sub SyncListProps2() _
                Implements TbImageListPrivate.SyncListProps
    
            SyncListProps
        End Sub
        
        [Serialize(False)]
        Public Property Get ImageWidth() As Long
            Return InternalImageWidth
        End Property

        [Serialize(False)]
        Public Property Let ImageWidth(Value As Long)
            ImageWidthPrivate = Value
        End Property
        
        [Serialize(False)]
        Protected Property Let ImageWidthPrivate(Value As Long)
            If Value < 0 Then Err.Raise 380, , "Invalid property value"
            If Me.InternalListImages.hImageList <> 0 Then Err.Raise 35611, , "Property is read-only if image list contains images"
            InternalImageWidth = Value
        End Property
        
        [Serialize(False)]
        Public Property Get ImageHeight() As Long
            Return InternalImageHeight
        End Property
        
        [Serialize(False)]
        Public Property Let ImageHeight(Value As Long)
            ImageHeightPrivate = Value
        End Property
        
        [Serialize(False)]
        Protected Property Let ImageHeightPrivate(Value As Long)
            If Value < 0 Then Err.Raise 380, , "Invalid property value"
            If Me.InternalListImages.hImageList <> 0 Then Err.Raise 35611, , "Property is read-only if image list contains images"
            InternalImageHeight = Value
        End Property
        
        Protected Function HandleGetScaleX() As Double _
                Implements TbImageListPrivate.GetScaleX
            Return ControlContext.RuntimeUICtxGetScaleModePixelsMultiplierX_SELF()
        End Function
        
        Protected Function HandleGetScaleY() As Double _
                Implements TbImageListPrivate.GetScaleY
            Return ControlContext.RuntimeUICtxGetScaleModePixelsMultiplierY_SELF()
        End Function
        
        Protected Sub HandleSetWidthHeight(ByVal width As Long, ByVal height As Long) _
                Implements TbImageListPrivate.SetWidthHeight
            ImageWidth_INIT = width
            ImageHeight_INIT = height
            
            InternalImageWidth = width
            InternalImageHeight = height
        End Sub
        
        Protected Sub HandleGetInitImageData(Images() As String, Keys() As String, Tags() As String) _
                Implements TbImageListPrivate.GetInitImageData
            Images = InternalImages_INIT
            Keys = InternalKeys_INIT
            Tags = InternalTags_INIT
        End Sub
        
        Protected Sub HandleSetInitImageData(Images() As String, Keys() As String, Tags() As String) _
                Implements TbImageListPrivate.SetInitImageData
            InternalImages_INIT = Images
            InternalKeys_INIT = Keys
            InternalTags_INIT = Tags
        End Sub
        
        [Serialize(False)]
        Public Property Get ListImages() As ListImages
            Return InternalListImages
        End Property
        
        Protected Property Get HandleImageWidth() As Long _
                Implements TbImageListPrivate.ImageWidth
            Return Me.InternalImageWidth
        End Property
        Protected Property Let HandleImageWidth(ByVal Value As Long) _
                Implements TbImageListPrivate.ImageWidth
            Me.InternalImageWidth = Value
        End Property
        Protected Property Get HandleImageHeight() As Long _
                Implements TbImageListPrivate.ImageHeight
            Return Me.InternalImageHeight
        End Property
        Protected Property Let HandleImageHeight(ByVal Value As Long) _
                Implements TbImageListPrivate.ImageHeight
            Me.InternalImageHeight = Value
        End Property
        Protected Property Get HandleColorDepth() As ImageListColorDepth _
                Implements TbImageListPrivate.ColorDepth
            Return Me.ColorDepth
        End Property
        Protected Property Get HandleUseMaskColor() As Boolean _
                Implements TbImageListPrivate.UseMaskColor
            Return Me.UseMaskColor
        End Property
        Protected Property Get HandleMaskColor() As OLE_COLOR _
                Implements TbImageListPrivate.MaskColor
            Return Me.MaskColor
        End Property
        Protected Property Get HandleBoundCount() As Long _
                Implements TbImageListPrivate.BoundCount
            Return Me.BoundCount
        End Property
        
    #End Region
    
End Class

[WindowsControl("/miscellaneous/ICONS??/ImageList??.png")]
[ClassId("2F75D39C-A8E3-4D5E-844A-252BED31075B")]
[InterfaceId("D120535D-59D2-4E52-A66C-4B6D9CAB9D5D")]
[EventInterfaceId("40B40E37-E8A1-42E8-9A79-37F53E145404")]
[COMCreatable(False)]
[EventsUseDispInterface]
Class ImageList
    Inherits ImageListBaseCtl
    
    Protected Sub Class_BeforeFirstMethodAccess()
        [_HiddenModule].EnsureContainerIsLoaded(Me)
    End Sub
End Class
#End If